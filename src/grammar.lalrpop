use identifier::Ident;
use std::str::FromStr;
use types::Types;
use label::{Label, TyPath};
use term::{BinaryOp, RichTerm, Term, UnaryOp};

grammar;

pub Term: RichTerm = {
    "fun" <p:Pattern> "=>" <t:Term> =>
        RichTerm::new(Term::Fun(p, t)),
    "let" <id:Ident> "=" <t1:Term> "in" <t2:Term> =>
        RichTerm::new(Term::Let(id, t1, t2)),
    "if" <b:Term> "then" <t:Term> "else" <e:Term> =>
        RichTerm::app(RichTerm::app(RichTerm::new(Term::Op1(UnaryOp::Ite(), b)), t), e),
    Operation    
};

Operation: RichTerm = {
    <t1: Applicative> <op: BOp> <t2: Operation> => RichTerm::new(Term::Op2(op, t1, t2)),
    <op: UOp> <t: Operation> => RichTerm::new(Term::Op1(op, t)),
    Applicative,
};

Applicative: RichTerm = {
    <t1:Applicative> <t2:Atom> => RichTerm::new(Term::App(t1, t2)),
    Atom,
};

Atom: RichTerm = {
    "(" <Term> ")",
    <l:@L> "Promise(" <ty: Types> "," <t: Term> ")" <r:@R> =>
        RichTerm::new(Term::Promise(ty, Label{tag: "A promise".to_string(), l: l, r: r, polarity: true, path: TyPath::Nil()}, t)),
    <l:@L> "Assume(" <ty: Types> "," <t: Term> ")" <r:@R> =>
        RichTerm::new(Term::Assume(ty, Label{tag: "An assume".to_string(), l: l, r: r, polarity: true, path: TyPath::Nil()}, t)), 
    Num => RichTerm::new(Term::Num(<>)),
    Bool => RichTerm::new(Term::Bool(<>)),
    Ident => RichTerm::new(Term::Var(<>)),
};

Pattern: Ident = {
    Ident,
};

Ident: Ident = r"[[:alpha:]_][[:word:]-]*" => Ident(<>.to_string());

Num: f64 = r"[-+]?[0-9]*\.?[0-9]+" => f64::from_str(<>).unwrap();

Bool: bool = {
    "true" => true,
    "false" => false,
};

UOp: UnaryOp = {
    "isZero" => UnaryOp::IsZero(),
    "isNum" => UnaryOp::IsNum(),
    "isBool" => UnaryOp::IsBool(),
    "isFun" => UnaryOp::IsFun(),
    "blame" => UnaryOp::Blame(),
    "chngPol" => UnaryOp::ChangePolarity(),
    "goDom" => UnaryOp::GoDom(),
    "goCodom" => UnaryOp::GoCodom(),
    "tag[" <DbgStr> "]" => UnaryOp::Tag(<>),
};

BOp: BinaryOp = {
    "+" => BinaryOp::Plus(),
};

Types: Types = {
    <s: subType> "->" <t:Types> => Types::Arrow(Box::new(s), Box::new(t)),
    <subType>,
};

subType : Types = {
    "Dyn" => Types::Dyn(),
    "Num" => Types::Num(),
    "Bool" => Types::Bool(),
    "(" <Types> ")" => <>,
};

DbgStr: String = r"[[:alpha:]_][[:word:]-]*" => <>.to_string();
